<?xml version="1.0" encoding="utf-8"?>
<!--
 Aplicación Principal del Sistema 
 Fecha de creación: 7/2/2009
 Fecha de última modificación: 11/3/2009
 Autor: Jorge Fabián Mitre  (jorgemitre@hotmail.com)
 Descripción: Modelo de referencia para crear una aplicación FX con un menú vinculado a un
 viewstack y en cada vista la incorporación del módulo correspondiente, con la posibilidad
 de capturar el evento "eveModulosHttpsResult" el cual es disparado desde el manejador
 del evento "result" del objeto clase HTTPService, cuyo objeto es evaluar el control de acceso,
 como se puede apreciar en la función fncEveModulosHttpsResult 
 "SI TE SIRVE ESTE CODIGO, POR FAVOR, NO QUITES ESTAS LINEAS"
-->

<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" 
				layout="vertical" applicationComplete="fncInicio()" 
				 width="100%" height="100%"
				 xmlns:ns1="colas.*" xmlns:ns2="consulta.*"
				 xmlns:ns3="consultar_vademecum.*" xmlns:ns4="consultar_diagnosticos.*" 
				 xmlns:ns5="consultar_practicas.*"  xmlns:ns6="historial.*"  xmlns:ns7="cola_lotes.*" xmlns:ns8="generadorconsultas.*">

	<mx:Style source="simplicitygray.css"></mx:Style>
	
	<mx:Script>
		<![CDATA[
			import mx.charts.BubbleChart;
			import historial.buscar;
			import mx.events.MenuEvent;
			import flash.events.Event;
			import flash.display.*;
			import flash.net.URLRequest;
			import mx.events.CloseEvent;
			import paquete01.ControlAcceso;
			import paquete01.ControlAccesoPopUp
			import paquete01.ControlAccesoCambiarPasswordPopUp;
			import mx.controls.Alert;
			import mx.managers.PopUpManager;
			
			// *********************************************************************************
			// *********************************************************************************
			// C O N T R O L  D E  A C C E S O >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
			// ---------------------------------------------------------------------------------
			// Se define el objeto de la clase ControlAcceso que manejará el control de acceso:
		    [Bindable] public var controlAcceso : ControlAcceso;
		    // Se define la variable booleana que determina si el usuario está autorizado a operar el sistema:  
		    [Bindable] public var controlAccesoAutorizado : Boolean = false;
		    
		    [Bindable] public var xmlServiciosUser : XML;
		    
		    [Bindable] private var idPersona : String;
		    [Bindable] public var DesdeConsulta : Boolean = false;
		    private var buscarHistorial : buscar;
		    private var cargaLotes: Boolean = false;
		    private var abmVademecum:Boolean = false;
		    private var abmPracticas:Boolean = false;
		    
		    public function get abmVad():Boolean { return abmVademecum }
		    public function get abmPra():Boolean { return abmPracticas }
		    
			private function fncSelectPrincipal():void
			{
				if (cargaLotes == false) {
					viewModulos.selectedChild = viewPrincipal;
					this.COLA.fncTraerColas(null);
					this.COLA.fncIniciarTimer();	
				} else {
					viewModulos.selectedChild = viewLotes;
					this.COLALOTES.fncTraerColas(null);
					this.COLALOTES.fncIniciarTimer();
				}							
			}	
			// Función que es convocada cuando se presiona el botón "Login" del formulario de logueo:
			//               __________
			
            private function fncTraerServicios() : void
            //               __________
             { 
             	btnLogin.label = "Trayendo servicios...";
             	btnLogin.enabled = false;
             	cmbServicio.enabled = false;
             	cmbArea.enabled = false;
             	controlAcceso.TraerServicios(textInputUsuario.text);
             }
			public function fncEveServiciosTraidos(e:Event):void
			{
				btnLogin.label = "Login";
             	xmlServiciosUser=controlAcceso.xmlServicios;
             	if (xmlServiciosUser.hasOwnProperty('areaservicio')){
             		cmbServicio.enabled = true;
             		cmbArea.enabled = true;
             		btnLogin.enabled = true;
             	}
			}
			// Función que es convocada cuando se presiona el botón "Login" del formulario de logueo:
			//               __________
			
            private function fncLogin() : void
            //               __________
             { 
             	var id_OAS_usuario : String = cmbServicio.selectedItem.@id_oas_usuario;
             	btnLogin.label = "Validando...";
             	btnLogin.enabled = false;
             	controlAcceso.login(textInputUsuario.text, textInputPassword.text, id_OAS_usuario, '030', '030001');
             }  
             
            // Función que es convocada ante el "disparo" del evento "eveLogueoRealizado" de la clase ControlAcceso:
			//               ______________________________
			
			private function fncEveLogueoRealizado(e:Event):void
			//               ______________________________
            {
             	//---
             	controlAccesoAutorizado = controlAcceso.getAutorizado;
             	btnLogin.label = "Login";
             	btnLogin.enabled = true;
             	//---
             	if(controlAcceso.getAutorizado) {
             		menuPrincipal.enabled=true;
             	  	// Muestra en la parte del menu los datos del usuario que se logueó
             	  	txtDatosUsuario.htmlText = "<b><u>Usuario:</u></b> " + controlAcceso.getUsuario;
             	  	txtDatosUsuario.htmlText+= " (" + controlAcceso.getUsuarioNombre + ")<br>";
             	  	txtDatosUsuario.htmlText+= "<b><u>Area:</u></b> " + controlAcceso.getUsuarioOrganismoArea;
             	  	txtDatosUsuario.htmlText+= " (" + controlAcceso.getUsuarioOrganismo + ")";
             	  	txtDatosUsuario.htmlText+= "<br><b><u>Servicio:</u></b> " + controlAcceso.usuario_servicio;
             	  	//------------------------------------------------------------------
             	  if (controlAcceso.tienePerfil("030LTS")) {
             	  	cargaLotes = true;             	  	
             	  	viewModulos.selectedChild = viewLotes;
             	  } else {
             	  	cargaLotes = false;
             	  	viewModulos.selectedChild = viewPrincipal;
             	  }
             	  /*if (controlAcceso.tienePerfil("030VAD")) {
             	  	abmVademecum = true;
             	  } else {
             	  	abmVademecum = false;
             	  }
             	  if (controlAcceso.tienePerfil("030PRA")) {
             	  	abmPracticas = true;
             	  } else {
             	  	abmPracticas = false;
             	  }*/
             	}
            } 
            // Función que es convocada ante el "disparo" del evento "eveLogoutRealizado" de la clase ControlAcceso:
            //               ______________________________
             
			private function fncEveLogoutRealizado (e:Event):void
			//               ______________________________
             {
             	//---
             	controlAccesoAutorizado = controlAcceso.getAutorizado;
             	// Activo la vista del view stack en la q se solicita usuario y contraseña
             	viewModulos.selectedChild=viewControlAcceso;
             	// Deshabilito las opciones del menú del sistema
			   	menuPrincipal.enabled=false;
             	//txtUsuarioNombre.text=controlAcceso.getUsuarioNombre;
             	txtDatosUsuario.htmlText='';
             	textInputUsuario.text = '';
             	textInputPassword.text = '';             	
             }
             
             
            // Función que se convoca ante el disparo del evento eveLogueoImposible de la clase ControlAcceso
            // que sucede cuando el objeto de la clase HTTPService usado en la clase ControlAcceso
            // devuleve "fault", por ejemplo, porque se perdió la conexión al servidor, hubo un error
            // en el script php que se convocaba, etc. 
            //               __________________________________
            
            private function fncEveLogueoFalloConexion(e:Event):void
            //               __________________________________
             {
             	//---
             	controlAccesoAutorizado = controlAcceso.getAutorizado;
             	btnLogin.label = "Login";
             	btnLogin.enabled = true;
             	//---
             }
             
            // Esta función es la que debe convocarse con los parámetros _acceso y _login
            // obtenidos de control_acceso_felex.php y pasados a flex a través de algún HTTPService
            // ante el evento "Result" del mismo. 
            //               _________________________________________________________
            
            private function fncControlAccesoHttpsResult(_acceso:String,_login:String): void
            //               _________________________________________________________
             { 
			   if(_acceso == "NO")
			     {  
			     	controlAccesoAutorizado = false;
			     	menuPrincipal.enabled=false;
			     	viewModulos.selectedChild=viewControlAcceso;
			     }
             }
             
		    // Función inicial > > > > > > > > > > > > > > > > > > > > > > > > > > > > > > > > > >
            //              ___________
            
			public function fncInicio() : void
			//              ___________
			 {
			  // *********************************************************************************	
			  // *********************************************************************************
			  // C O N T R O L  D E  A C C E S O >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
			  // -------------------------------
			  // Al iniciarse la aplicación, instancio el objeto controlAcceso de la clase ControlAcceso:	
			  controlAcceso = new ControlAcceso;
			 
			  // Creo un evento propio del objeto controlAcceso denominado "eveLogueoRealizado", 
			  // el cual, cuando es disparado (desde la clase ControlAcceso que se encuentra en paquete01) 
			  // se convoca a la función "fncEveLogueoRealizado":
			  controlAcceso.addEventListener("eveLogueoRealizado",fncEveLogueoRealizado)
			  controlAcceso.addEventListener("eveLogueoFalloConexion",fncEveLogueoFalloConexion)
			  controlAcceso.addEventListener("eveLogoutRealizado",fncEveLogoutRealizado)
			  controlAcceso.addEventListener("eveServiciosTraidos",fncEveServiciosTraidos)
			 
			  
			  // *********************************************************************************
			  // C O N T R O L  D E  A C C E S O <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
			  // *********************************************************************************
			  
			  if (controlAcceso.getAutorizado==false)
			   {
			   	viewModulos.selectedChild=viewControlAcceso;
			   	viewModulos.visible=true;
			   	menuPrincipal.enabled=false;
			   }	
			 }
			// Fin funcion inicial < < < < < < < < < < < < < < < < < < < < < < < < < < < < < < 
			  
			 
			// ---------------------------------------------------------------------------------
			// Función que captura el evento "eveModulosHttpsResult", disparada
			// desde el manejador del evento "result" de los objetos de clase HTTPService 
			// que se encuentran en los módulos que son convocados por la aplicación principal.
			// > > > > > > > > > > > > > > > > > > > > > > > > > > > > > > > > > > > > > > > > > 
			//              ________________________________
			
			public function fncEveModulosHttpsResult(e:Event):void
			//              _________________________________
			 {
			 	var _acceso : String = (e.target.get_acceso);
			 	if(_acceso=="NO")
			 	  {
			     	controlAccesoAutorizado = false;
			     	menuPrincipal.enabled=false;
			     	viewModulos.selectedChild=viewControlAcceso
			 	  }
			 } 
           // < < < < < < < < < < < < < < < < < < < < < < < < < < < < < < < < < < < < < < < < < < 
		   private function fncOpcionMenu(e:MenuEvent):void
		    {
				this.verticalScrollPosition = 0;
				if (cargaLotes == false)
					this.COLA.fncDetenerTimer();
				else
					this.COLALOTES.fncDetenerTimer();
		    	switch(e.item.@id.toString())
				{	
				    case "vademecum":
				        viewModulos.selectedChild = viewVademecum;
				        break;
				   	case "cie10":
				        viewModulos.selectedChild = viewDiagnosticos;
				        break;
				    case "practicas":
				        viewModulos.selectedChild = viewPracticas;
				        break;
				    case "historial":
				         	buscarHistorial = new buscar;
				         	buscarHistorial.addEventListener("verHistorial",fncHistorialBusqueda);
				         	PopUpManager.addPopUp(buscarHistorial,this,true);
             				PopUpManager.centerPopUp(buscarHistorial);
				        break;
				    case "generador":
				    	if (controlAcceso.tienePerfil("030EME") || controlAcceso.tienePerfil("030EHO")) {
				    		viewModulos.selectedChild = viewGeneradorConsultas;
				    	} else {
				    		Alert.show("Usted no posee el perfil necesario para acceder a esta opción.","M E N U");
				    	}
				    	break;
				    case "cambiarPassword":
				    	var cambiarPasswordPopUp : ControlAccesoCambiarPasswordPopUp = new ControlAccesoCambiarPasswordPopUp;
             			PopUpManager.addPopUp(cambiarPasswordPopUp,this,true);
             			PopUpManager.centerPopUp(cambiarPasswordPopUp);
				        break;
				    case "logout":
				        viewModulos.selectedChild = viewSalir;
				        break;				 				        
				    default:
				        Alert.show("Opción (" + e.item.@id + ") aún no implementada.","M E N U");		    			
				        break;
				}
		    }	
		    
		    //funcion que llama al modulo consulta pasandole como parametro el id del turno
		    private function fncFicha(e:Event):void
		    { 
		    	viewModulos.selectedChild = viewConsulta;
		    	if (CONSULTA){
		    		if (cargaLotes == false)
		    			CONSULTA.fncInit(COLA.idIngreso);
		    		else
		    			CONSULTA.fncInit(COLALOTES.idIngreso);
		    	}
		    }
		    //inicializa el modulo consulta para realizar una nueva
		    private function fncInitConsulta():void
		    {
		    	if (cargaLotes == false)
		    		CONSULTA.fncInit(COLA.idIngreso);
		    	else
		    		CONSULTA.fncInit(COLALOTES.idIngreso);
		    }
		    
		    private function fncViewHistorial():void{
		    	viewModulos.selectedChild = viewHistorial;
		    	if (HISTORIAL){
		    		HISTORIAL.idPersona = CONSULTA.xmlDatosPaciente.paciente.@id_persona;
		    		HISTORIAL.fncInit();
		    	}
		    	idPersona = CONSULTA.xmlDatosPaciente.paciente.@id_persona;
		    	DesdeConsulta = true;
		    }
		    
		    private function fncInitHistorial():void{
		    	HISTORIAL.idPersona = idPersona;
		    	HISTORIAL.fncInit();
		    }
		    
		    private function fncHistorialBusqueda(e:Event):void{
		    	viewModulos.selectedChild = viewHistorial;
		    	if (HISTORIAL){
		    		HISTORIAL.idPersona = buscarHistorial.idPersona;
		    		HISTORIAL.fncInit();
		    	}
		    	idPersona = buscarHistorial.idPersona;
		    	DesdeConsulta = false;
		    	PopUpManager.removePopUp(e.target as buscar);
		    }
		    
		    private function fncSelectConsulta():void{
		    	viewModulos.selectedChild = viewConsulta;
		    }
		]]>
	</mx:Script>

	<!--
	< V I E W S T A C K >> >> >> >> >> >> >> >> >> >> >> >> >> >> >> >> >> >> >> >> >> >> >> >> >> >> >> >>
	-->		
	<mx:ViewStack id="viewModulos" width="100%" height="100%" borderColor="#740611">
		<!--
		<< v i e w:   L O G U E O > > > > > > > > > > > > > > > > > > > > > > > > > >
		-->		
		<mx:Canvas label="Logueo" width="100%" height="100%" id="viewControlAcceso">
			<mx:Panel layout="absolute" id="pnlControlAcceso" title="Acceso al Sistema" horizontalAlign="center" horizontalCenter="0" verticalCenter="-53">
				<mx:Form x="10" y="10" width="100%" height="138">
					<mx:FormItem label="Usuario">
						<mx:TextInput focusOut="fncTraerServicios()" id="textInputUsuario" width="120"/>
					</mx:FormItem>
					<mx:FormItem label="Password">
						<mx:TextInput id="textInputPassword" displayAsPassword="true" width="119"/>
					</mx:FormItem>
					<mx:FormItem label="Area">
						<mx:ComboBox dataProvider="{xmlServiciosUser.areaservicio}" labelField="@area" enabled="false" editable="false" id="cmbArea" width="285"></mx:ComboBox>
					</mx:FormItem>
					<mx:FormItem label="Servicio">
						<mx:ComboBox dataProvider="{xmlServiciosUser.areaservicio.(@id_area==cmbArea.selectedItem.@id_area).servicio}" labelField="@servicio" enabled="false" editable="false" id="cmbServicio" width="285"></mx:ComboBox>
					</mx:FormItem>
				</mx:Form>
				<mx:ControlBar horizontalAlign="right">
					<mx:Button label="?" 
						       labelPlacement="left" 
						       id="btnLoginHelp" fontWeight="normal" alpha="0.7">
					 <mx:click>
			 	<![CDATA[
			 	    var txtDetallesAcceso:String='';
			 	    if (controlAcceso.getAutorizado)
			 	      {
			 	      	txtDetallesAcceso+= "Posé acceso al sistema. ";
			 	      }
			 	     else
			 	      {
			 	      	if (controlAcceso.getUsuario)
			 	      	 {
			 	      	  txtDetallesAcceso+= "El usuario "+controlAcceso.getUsuario+" NO posé acceso al sistema. ";
			 	      	 }
			 	      	else
			 	      	 {
			 	      	  txtDetallesAcceso+= "No intentó loguearse aún. ";
			 	      	 }  
			 	      } 
			 	      
			 		if(controlAcceso.getMensajeError)
			 		  {
			 		  	
			 		  	Alert.show(txtDetallesAcceso+" "+controlAcceso.getMensajeError,"ERROR DE ACCESO AL SISTEMA:");
			 		  }
			 		 else
			 		  {
			 		  	Alert.show(txtDetallesAcceso+" "+"Ingrese sus datos de acceso (usuario y contraseña) respetando mayúsculas y minúsculas para iniciar una nueva sesión o si desea cambiar de usuario.","ACCESO AL SISTEMA");
			 		  	
			 		  }
			 	]]>
			 </mx:click>	       
					   </mx:Button>
					<mx:Button label="Login" 
						       labelPlacement="left" 
						       id="btnLogin" 
						       click="fncLogin()" />
				</mx:ControlBar>
			</mx:Panel>
		</mx:Canvas>
		<!--
		<< v i e w:  Pantalla Principal  > > > > > > > > > > > > > > > > > > > > > > > > > >
		-->	
		<mx:VBox id="viewPrincipal" width="100%" height="100%">
			<ns1:colas id="COLA" 
				EventAtenderPaciente="fncFicha(event)" 
				EventModificarConsulta="fncFicha(event)" 
				eveModulosHttpsResult="fncEveModulosHttpsResult(event)" />	
		</mx:VBox>
		<!--
		<< v i e w:  Pantalla Principal Carga en Lotes  > > > > > > > > > >
		-->
		<mx:Canvas id="viewLotes" width="100%" height="100%">
			<ns7:cola_lotes id="COLALOTES"
				EventAtenderPaciente="fncFicha(event)" 
				EventModificarConsulta="fncFicha(event)" 
				eveModulosHttpsResult="fncEveModulosHttpsResult(event)" />
		</mx:Canvas>
		
		<!--
		<< v i e w:  CONSULTA  > > > > > > > > > > > > > > > > > > > > > > > > > >
		-->	
		<mx:Canvas id="viewConsulta" width="100%" height="100%">
			<ns2:consulta id="CONSULTA" ViewHistorial="fncViewHistorial()" creationComplete="fncInitConsulta()"  SelectPrincipal="fncSelectPrincipal()" eveModulosHttpsResult="fncEveModulosHttpsResult(event)" />	
		</mx:Canvas>
		<!--
		<< v i e w:  CONSULTAR VADEMECUM > > > > > > > > > > > > > > > > > > > > > > > > > >
		-->	
		<mx:Canvas id="viewVademecum" width="100%" height="100%">
			<ns3:vademecum id="VADEMECUM" SelectPrincipal="fncSelectPrincipal()" eveModulosHttpsResult="fncEveModulosHttpsResult(event)" />	
		</mx:Canvas>
		
		<!--
		<< v i e w:  CONSULTAR PRACTICAS > > > > > > > > > > > > > > > > > > > > > > > > > >
		-->	
		<mx:Canvas id="viewPracticas" width="100%" height="100%">
			<ns5:practicas id="PRACTICAS" SelectPrincipal="fncSelectPrincipal()" eveModulosHttpsResult="fncEveModulosHttpsResult(event)" />	
		</mx:Canvas>
		
		<!--
		<< v i e w:  CONSULTAR DIAGNOSTICOS > > > > > > > > > > > > > > > > > > > > > > > > >
		-->	
		<mx:Canvas id="viewDiagnosticos" width="100%" height="100%">
			<ns4:cie10 id="CIE10" SelectPrincipal="fncSelectPrincipal()" eveModulosHttpsResult="fncEveModulosHttpsResult(event)" />	
		</mx:Canvas>
		
		<!--
		<< v i e w:  CONSULTAR HISTORIAL > > > > > > > > > > > > > > > > > > > > > > > > > >
		-->
		<mx:Canvas width="100%" height="100%" id="viewHistorial">
			<ns6:historial id="HISTORIAL" creationComplete="fncInitHistorial()" SelectConsulta="fncSelectConsulta()" SelectPrincipal="fncSelectPrincipal()" eveModulosHttpsResult="fncEveModulosHttpsResult(event)" />
		</mx:Canvas>
		<mx:Canvas label="generador" width="100%" height="100%" id="viewGeneradorConsultas">
			<ns8:generadorconsultas SelectPrincipal="fncSelectPrincipal()" horizontalCenter="0" top="10">
			</ns8:generadorconsultas>
		</mx:Canvas>
		
		<!--
		<< v i e w:  C I E R R E  D E  S E S I O N > > > > > > > > > > > > > > > > > > > > > > > > > >
		 -->
		<mx:Canvas label="Salir" width="100%" height="100%" id="viewSalir" >
		  <mx:show>
		  	<![CDATA[
		  	    lblCerrandoSesion.htmlText = "<i>Cerrando Sesión de <b>" + controlAcceso.getUsuario + "</b>...</i>";
		  	    controlAcceso.logout();
		  	]]>
		  </mx:show>
		  <mx:Label id="lblCerrandoSesion" htmlText="Cerrando Sesion..." fontSize="15" x="254.5" y="10">
		  </mx:Label>
		</mx:Canvas>	
	</mx:ViewStack> 
	<!--
	Fin V I E W S T A C K << << << << << << << << << << << << << << << << << << << << << << << << << << <<  
	-->
	<mx:ApplicationControlBar dock="true" height="53">
	
		<mx:Grid>
			<mx:GridRow width="100%" height="14">
				<mx:GridItem width="100%" height="100%">
					<mx:Label text="SURAM" fontWeight="bold"/>
				</mx:GridItem>
			</mx:GridRow>
			<mx:GridRow width="100%" height="25">
				<mx:GridItem width="100%" height="100%">
				
				    <!-- M E N U  P R I N C I P A L >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> -->
					<mx:MenuBar labelField="@etiqueta" itemClick="fncOpcionMenu(event)" id="menuPrincipal">
						<mx:XMLList xmlns="">
							<menuitem etiqueta="Pacientes" id="consultar">
							 	<menuitem etiqueta="Ver Historial" id="historial" />
							</menuitem>
							<menuitem etiqueta="Consultar..." id="consultar">
							 	<menuitem etiqueta="Vademecum" id="vademecum" />
							 	<menuitem etiqueta="Diagnosticos" id="cie10" />
							 	<menuitem etiqueta="Nomencaldor Practicas y Estudios" id="practicas" />
							</menuitem>
							<menuitem etiqueta="Estadísticas" id="estadisticas">
								<menuitem etiqueta="Generador de Consultas" id="generador" />
							</menuitem>							
							<menuitem etiqueta="Usuario" id="usuario">
							 	<menuitem etiqueta="Cerrar Sesión" id="logout" />
							 	<menuitem etiqueta="Cambiar Contraseña" id="cambiarPassword" />
							</menuitem> 
						</mx:XMLList>
					</mx:MenuBar>
				    <!-- M E N U  P R I N C I P A L <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< -->
					
				</mx:GridItem>
			</mx:GridRow>
		</mx:Grid>
		<mx:Text id="txtDatosUsuario" height="100%" width="100%" fontSize="9"  textAlign="center" />
		
	</mx:ApplicationControlBar>
</mx:Application>